# Copyright (c) 2005-2009 Jaroslav Gresula
#
# Distributed under the MIT license (See accompanying file
# LICENSE.txt or copy at http://jagpdf.org/LICENSE.txt)
#



if(JAG_STANDALONE_APITESTS AND NOT JAG_APITEST_C)
  return()
endif()

#
# API tests for c++
#
# - all test names prefixed with api_cpp_
#
c_cpp_test_preamble(pdf-output/cpp)


#
# -- unit tests driven by the driver
#
create_test_sourcelist(Tests cpptestdriver.cpp
  helloworld.cpp
  basic.cpp
  docoutline.cpp
  annots.cpp
  externalstream.cpp
  config.cpp
  proxyclasstest.cpp
  version.cpp
  textshow.cpp
  defaultargs.cpp
  except.cpp
  fonts.cpp
  autodetectimg.cpp
  EXTRA_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/testcommon.cpp
)

add_executable(api-cpp-tests-driver ${Tests})
target_link_libraries(api-cpp-tests-driver jagpdf-bin-c)

set(TestsToRun ${Tests})
remove(TestsToRun cpptestdriver.cpp)

foreach(test ${TestsToRun})
  get_filename_component(TName ${test} NAME_WE)
  add_test(api_cpp_${TName} ${CXX_TEST_PATH}/api-cpp-tests-driver ${TName} ${TEST_PDF_OUTPUT_DIR})
endforeach()



#
# -- tests C++ API with disabled exceptions
#
# TBD: ideally, it should be compiled without exception support
#
jag_append_source_flag(noexcept.cpp COMPILE_DEFINITIONS JAG_DO_NOT_USE_EXCEPTIONS)
add_executable(no-execept noexcept.cpp)
target_link_libraries(no-execept jagpdf-bin-c)
add_test(api_cpp_noexcept ${CXX_TEST_PATH}/no-execept ${TEST_PDF_OUTPUT_DIR})



#
# -- main target apit-cpp-tests
#
#  - builds the necessary targets
#  - removes all PDF files from the output directory
#  - updates .outfiles in the output directory
#  - runs text prefixed with api_cpp_ via ctest
#  - checks the generated PDF files
#
macro(apitests_cpp_generic NAME MEMCHECK)
  add_custom_target(${NAME}
    DEPENDS api-cpp-tests-driver no-execept
    COMMAND ${CMAKE_COMMAND} -E remove ${TEST_PDF_OUTPUT_DIR}/*.pdf
    COMMAND ${CMAKE_COMMAND} -E copy_if_different ${CMAKE_CURRENT_SOURCE_DIR}/.outfiles ${TEST_PDF_OUTPUT_DIR}/.outfiles
    COMMAND ${JAG_MYCTEST_SH} ${MEMCHECK} ${CMAKE_CTEST_COMMAND} -R api_cpp_
    COMMAND ${CMAKE_COMMAND} -E echo "-- Verifying PDF files generated by target apitests-cpp"
    COMMAND ${JAG_COMPARE_PDF} --verify-list --out-dir=${TEST_PDF_OUTPUT_DIR} --good-dir=${PDF_REFERENCE_DIR}
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}"
    )
endmacro(apitests_cpp_generic)

apitests_cpp_generic(apitests-cpp "")
apitests_cpp_generic(apitests-cpp-memcheck "${JAG_HAVE_MEMCHECK}")

add_dependencies(apitests apitests-cpp)
add_dependencies(apitests-memcheck apitests-memcheck)







