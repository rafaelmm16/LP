# Copyright (c) 2005-2009 Jaroslav Gresula
#
# Distributed under the MIT license (See accompanying file
# LICENSE.txt or copy at http://jagpdf.org/LICENSE.txt)
#


if(JAG_STANDALONE_APITESTS AND NOT JAG_APITEST_C)
  return()
endif()

#
# API tests for c
#
# - all test names prefixed with api_c
#
c_cpp_test_preamble(pdf-output/c)

#
# -- unit tests driven by the driver
#
create_test_sourcelist(Tests ctestdriver.c
  basic.c
  version.c
  externalstream.c
  errhandling.c
  helloworld.c
  EXTRA_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/ctestcommon.c
)

add_executable(api-c-test-driver ${Tests})
target_link_libraries(api-c-test-driver jagpdf-bin-c)

set(TestsToRun ${Tests})
remove(TestsToRun ctestdriver.c)

foreach(test ${TestsToRun})
  get_filename_component(TName ${test} NAME_WE)
  add_test(api_c_${TName} ${CXX_TEST_PATH}/api-c-test-driver ${TName} ${TEST_PDF_OUTPUT_DIR})
endforeach()



#
# -- main target apit-cpp-tests
#
#  - builds the necessary targets
#  - removes all PDF files from the output directory
#  - updates .outfiles in the output directory
#  - runs text prefixed with api_cpp_ via ctest
#  - checks the generated PDF files
#
macro(apitests_c_generic NAME MEMCHECK)
  add_custom_target(${NAME}
    DEPENDS api-c-test-driver
    COMMAND ${CMAKE_COMMAND} -E remove ${TEST_PDF_OUTPUT_DIR}/*.pdf
    COMMAND ${CMAKE_COMMAND} -E copy_if_different ${CMAKE_CURRENT_SOURCE_DIR}/.outfiles ${TEST_PDF_OUTPUT_DIR}/.outfiles
    COMMAND ${JAG_MYCTEST_SH} ${MEMCHECK} ${CMAKE_CTEST_COMMAND} -R api_c_
    COMMAND ${CMAKE_COMMAND} -E echo "-- Verifying PDF files generated by target apitests-c"
    COMMAND ${JAG_COMPARE_PDF} --verify-list --out-dir=${TEST_PDF_OUTPUT_DIR} --good-dir=${PDF_REFERENCE_DIR}
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}"
    )
endmacro(apitests_c_generic)

apitests_c_generic(apitests-c "")
apitests_c_generic(apitests-c-memcheck "${JAG_HAVE_MEMCHECK}")

add_dependencies(apitests apitests-c)
add_dependencies(apitests-memcheck apitests-c-memcheck)





